#!/bin/zsh
set -euo pipefail
ROOT=${0:A:h:h}
APP=${GAO_PACKAGE_APP:-"$ROOT/build.noindex/搞节奏.app"}
STAGE="$ROOT/.release.noindex"
RELEASE_APP="$STAGE/搞节奏.app"
SIGNING_IDENTITY=${GAO_SIGNING_IDENTITY:-A15B2B2D22B018CBC842063C9A8A589946647524}
codesign --verify --deep --strict "$APP"
VERSION=$(/usr/libexec/PlistBuddy -c 'Print :CFBundleShortVersionString' "$APP/Contents/Info.plist")
[[ "$VERSION" == "$(/usr/libexec/PlistBuddy -c 'Print :CFBundleShortVersionString' "$ROOT/Info.plist")" ]] || { echo '构建版本与源码版本不一致';exit 1;}
rm -rf "$STAGE"
mkdir -p "$STAGE" "$ROOT/dist"
trap 'rm -rf "$STAGE"' EXIT
ditto "$APP" "$RELEASE_APP"
python3 "$ROOT/scripts/sanitize_bundle.py" "$RELEASE_APP"
for helper in "$RELEASE_APP"/Contents/Frameworks/Rhythm.framework/Versions/A/Resources/UsageEngine/tokscale(N); do
    codesign --force --sign "$SIGNING_IDENTITY" --options runtime "$helper"
done
codesign --force --sign "$SIGNING_IDENTITY" "$RELEASE_APP/Contents/Frameworks/Rhythm.framework"
codesign --force --sign "$SIGNING_IDENTITY" --entitlements "$ROOT/Widget.entitlements" "$RELEASE_APP/Contents/PlugIns/GaoJieZouWidgets.appex"
codesign --force --sign "$SIGNING_IDENTITY" --entitlements "$ROOT/App.entitlements" "$RELEASE_APP"
codesign --verify --deep --strict "$RELEASE_APP"
ditto -c -k --keepParent --norsrc --noextattr "$RELEASE_APP" "$ROOT/dist/搞节奏-$VERSION.zip"
(cd "$ROOT/dist"; shasum -a 256 "搞节奏-$VERSION.zip" > "搞节奏-$VERSION.zip.sha256")
echo "Packaged $ROOT/dist/搞节奏-$VERSION.zip"
